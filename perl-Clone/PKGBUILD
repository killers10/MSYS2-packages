# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=Clone
pkgname=perl-${_realname}
pkgver=0.46
pkgrel=1
pkgdesc='Recursive copy of nested objects.'
arch=('i686' 'x86_64')
url='https://search.cpan.org/~RDF/Clone'
groups=('perl-modules')
license=('GPL' 'PerlArtistic')
depends=('perl')
makedepends=('libcrypt-devel' 'gcc' 'make')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/G/GA/GARU/${_realname}-${pkgver}.tar.gz")
sha512sums=('f8bb1010364e94c7cc8bba25681cd9fd737ec2935a8be960ac53099359729fc679190a115dd082fccd239b35762dee2b3be3adbddce37e4ceae6fe934fbad545')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd ${_realname}-${pkgver}
    /usr/bin/perl Makefile.PL
    make
  )
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="$pkgdir" install
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
